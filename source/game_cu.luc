module game_cu (
    input clk,
    input fsm_clk,  // clock
    input fsm_clk2,
    input rst,  // reset
    input regfile_rd2[32], //direct reading of regfile data from rd2
    input decrease_timer,
    
    input p1_button_high,
    input p1_button_low,
    input p1_button_between,
    input p2_button_high,
    input p2_button_low,
    input p2_button_between,
    
    output alufn[6],
    output asel[2],
    output bsel[2],
    output wdsel[2],
    output rngsel[2],
    output rngout[4],
    output regfile_wa[4],
    output regfile_ra1[4],
    output regfile_ra2[4],
    output regfile_we,
    output state_debug_out[8],
    output rngsel[2]
) {
    enum GameStates{
        RESET_P1SCORE,
        RESET_P2SCORE,
        RESET_TIMER,
        RESET_LOW,
        RESET_HIGH,
        RESET_GUESS,
        START_TIMER,
        GENERATE_FIRST,
        GENERATE_SECOND,
        COMPARE_NUMBERS,
        BRANCH_COMPARE_NUMBERS,
        STORE_LOW_FIRST,
        STORE_HIGH_SECOND,
        STORE_HIGH_FIRST,
        STORE_LOW_SECOND,
        GENERATE_GUESS,
        CHECK_GUESS_LOW,
        BRANCH_GUESS_LOW,
        STORE_LOW_GUESS,
        CHECK_GUESS_HIGH,
        BRANCH_GUESS_HIGH,
        STORE_HIGH_GUESS,
        STORE_IN_BETWEEN,
        IDLE,
        SET_P1_BUTTON_LOW,
        SET_P1_BUTTON_HIGH,
        SET_P1_BUTTON_IN_BETWEEN,
        SET_P2_BUTTON_LOW,
        SET_P2_BUTTON_HIGH,
        SET_P2_BUTTON_IN_BETWEEN,
        DECREASE_GAME_TIMER,
        CHECK_TIMER,
        BRANCH_CHECK_TIMER,
        CHECK_P1_BUTTON_PRESS,
        BRANCH_P1_BUTTON_PRESS,
        INCREASE_P1_SCORE,
        CHECK_P2_BUTTON_PRESS,
        BRANCH_P2_BUTTON_PRESS,
        INCREASE_P2_SCORE,
        COMPARE_SCORES,
        BRANCH_COMPARE_SCORES,
        CHECK_P1_SCORE,
        BRANCH_CHECK_P1_SCORE,
        CHECK_P2_SCORE,
        BRANCH_CHECK_P2_SCORE,
        CHECK_DRAW,
        BRANCH_DRAW,
        DRAW,
        CHECK_WINNER,
        BRANCH_WINNER,
        P1_WINS,
        P2_LOSE,
        P2_WINS,
        P1_LOSE,
        GAMEOVER
    }

    dff game_fsm[$width(GameStates)](#INIT(GameStates.RESET_P1SCORE), .clk(clk), .rst(rst))
    dff debug[8](.clk(clk), .rst(rst), #INIT(0))
    sig advance 
    sig advance2
    
    always {
        advance = fsm_clk
        advance2 = fsm_clk2
        debug.d = debug.q
        state_debug_out = debug.q 
        
        // standard setting unless otherwise overwritten by each case 
        alufn = 0
        asel = 0 
        bsel = 0
        regfile_we = 0
        regfile_wa = d9
        regfile_ra1 = d0
        regfile_ra2 = d0
        wdsel = 0
        rngsel = 0
                
        game_fsm.d = game_fsm.q
        if (rst){
            game_fsm.d = GameStates.RESET_P1SCORE
        }
        else{
            
            case(game_fsm.q){
                
                GameStates.RESET_P1SCORE: 
                    alufn = b000000    //ADD 
                    asel = b10        // Bit 0
                    bsel = b10        // Bit 0
                    regfile_we = b1
                    regfile_wa = d0    // P1 Score Reg
                    wdsel = b00
                    if (advance) {
                        regfile_we = b1
                        game_fsm.d = GameStates.RESET_P2SCORE
                    }
                    debug.d = 8d0
                    
                GameStates.RESET_P2SCORE: 
                    alufn = b000000    //ADD 
                    asel = b10        // Bit 0
                    bsel = b10        // Bit 0
                    regfile_wa = d1    // P2 Score Reg
                    wdsel = b00
                    if (advance) {
                        regfile_we = b1
                        game_fsm.d = GameStates.RESET_TIMER
                    }
                    debug.d = 8d1
                
                GameStates.RESET_TIMER: 
                    alufn = b000000    //ADD 
                    asel = b10        // Bit 0
                    bsel = b10        // Bit 0
                    regfile_wa = d7    // Timer Reg
                    wdsel = b00
                    if (advance) {
                        regfile_we = b1
                        game_fsm.d = GameStates.RESET_LOW
                    }
                    debug.d = 8d2
                
                GameStates.RESET_LOW: 
                    alufn = b000000    //ADD 
                    asel = b10        // Bit 0
                    bsel = b10        // Bit 0
                    regfile_wa = d4    // Low Bound Reg
                    wdsel = b00
                    if (advance) {
                        regfile_we = b1
                        game_fsm.d = GameStates.RESET_HIGH
                    }
                    debug.d = 8d3
                
                GameStates.RESET_HIGH: 
                    alufn = b000000    //ADD 
                    asel = b10        // Bit 0
                    bsel = b10        // Bit 0
                    regfile_we = b1
                    regfile_wa = d5    // High Bound Reg
                    wdsel = b00
                    if (advance) {
                        regfile_we = b1
                        game_fsm.d = GameStates.RESET_GUESS
                    }
                    debug.d = 8d4
                
                GameStates.RESET_GUESS: 
                    alufn = b000000    //ADD 
                    asel = b10        // Bit 0
                    bsel = b10        // Bit 0
                    regfile_we = b1
                    regfile_wa = d6    // Guess Number Reg
                    wdsel = b00
                    if (advance) {
                        regfile_we = b1
                        game_fsm.d = GameStates.START_TIMER
                    }
                    debug.d = 8d5
                
                GameStates.START_TIMER: 
                    alufn = b000000    //ADD 
                    asel = b11        // Bit 5
                    bsel = b00        
                    regfile_ra2 = d7    // Timer Reg
                    regfile_we = b1
                    regfile_wa = d7    // Store to Timer Reg
                    wdsel = b00
                    game_fsm.d = GameStates.GENERATE_FIRST
                    debug.d = 8d6
                
                GameStates.GENERATE_FIRST: 
                    regfile_we = b1
                    regfile_wa = d8    // Store to Temp Reg 1
                    rngsel = b1
                    if (advance) {
                        regfile_we = b1
                        game_fsm.d = GameStates.GENERATE_SECOND
                    }
                    debug.d = 8d7
                
                GameStates.GENERATE_SECOND: 
                    regfile_we = b1
                    regfile_wa = d9    // Store to Temp Reg 2
                    rngsel = b1
                    if (advance) {
                        regfile_we = b1
                        game_fsm.d = GameStates.COMPARE_NUMBERS
                    }
                    debug.d = 8d8
                
                
                GameStates.COMPARE_NUMBERS: 
                    alufn = b110101    // CMPLT, Temp Reg 1 < Temp Reg 2
                    asel = b00
                    bsel = b00
                    regfile_ra1 = d8    // Temp Reg 1, Storing First Number
                    regfile_ra2 = d9    // Temp Reg 2, Storing Second Number
                    regfile_we = b1
                    regfile_wa = d10    // Store to Temp Reg 3
                    wdsel = b00
                    rngsel = b0
                    if (advance) {
                        regfile_we = b1
                        game_fsm.d = GameStates.BRANCH_COMPARE_NUMBERS
                    }
                    debug.d = 8d9
                
                GameStates.BRANCH_COMPARE_NUMBERS: 
                    regfile_ra2 = d10    // Temp Reg 3, containing the result of comparison, 1 means reg8 < reg9
                    if (regfile_rd2[0]) {    // If value in Reg 8 is lower than Reg 9, store low first
                        game_fsm.d = GameStates.STORE_LOW_FIRST
                    } else {
                        game_fsm.d = GameStates.STORE_HIGH_FIRST
                    }
                    debug.d = 8d10
                
                GameStates.STORE_LOW_FIRST:
                    alufn = b000000    //ADD 
                    asel = b00
                    bsel = b10        // Bit 0
                    regfile_we = b1
                    regfile_ra1 = d8    // Temp Reg 1, Storing First Number
                    regfile_wa = d4    // Low Bound Reg
                    wdsel = b00
                    if (advance) {
                        regfile_we = b1
                        game_fsm.d = GameStates.STORE_HIGH_SECOND
                    }
                    debug.d = 8d11
                
                GameStates.STORE_HIGH_SECOND:
                    alufn = b000000    //ADD 
                    asel = b00
                    bsel = b10        // Bit 0
                    regfile_we = b1
                    regfile_ra1 = d9    // Temp Reg 2, Storing Second Number
                    regfile_wa = d5    // High Bound Reg
                    wdsel = b00
                    if (advance) {
                        regfile_we = b1
                        game_fsm.d = GameStates.GENERATE_GUESS
                    }
                    debug.d = 8d12
                
                GameStates.STORE_HIGH_FIRST:
                    alufn = b000000    //ADD 
                    asel = b00
                    bsel = b10        // Bit 0
                    regfile_we = b1
                    regfile_ra1 = d8    // Temp Reg 1, Storing First Number
                    regfile_wa = d5    // High Bound Reg
                    wdsel = b00
                    if (advance) {
                        regfile_we = b1
                        game_fsm.d = GameStates.STORE_LOW_SECOND
                    }
                    debug.d = 8d13
                
                GameStates.STORE_LOW_SECOND:
                    alufn = b000000    //ADD 
                    asel = b00
                    bsel = b10        // Bit 0
                    regfile_we = b1
                    regfile_ra1 = d9    // Temp Reg 2, Storing Second Number
                    regfile_wa = d4    // Low Bound Reg
                    wdsel = b00
                    if (advance) {
                        regfile_we = b1
                        game_fsm.d = GameStates.GENERATE_GUESS
                    }
                    debug.d = 8d14
                
                GameStates.GENERATE_GUESS: 
                    regfile_we = b1
                    regfile_wa = d6    // Store to Guess Reg
                    rngsel = b1
                    if (advance) {
                        regfile_we = b1
                        game_fsm.d = GameStates.CHECK_GUESS_LOW
                    }
                    debug.d = 8d15
                
                GameStates.CHECK_GUESS_LOW:
                    alufn = b110101    // CMPLT, Check if Guess Reg < Low Bound Reg
                    asel = b00
                    bsel = b00        
                    regfile_we = b1
                    regfile_ra1 = d6    // Guess Reg
                    regfile_ra2 = d4    // Low Bound Reg
                    regfile_wa = d10    // Temp Reg 3
                    wdsel = b00
                    rngsel = 0
                    if (advance) {
                        regfile_we = b1
                        game_fsm.d = GameStates.BRANCH_GUESS_LOW
                    }
                    debug.d = 8d16
                
                GameStates.BRANCH_GUESS_LOW: 
                    regfile_ra2 = d10    // Temp Reg 3, 1 means guess number is smaller than low bound
                    if (regfile_rd2[0]) {
                        game_fsm.d = GameStates.STORE_LOW_GUESS
                    } else {
                        game_fsm.d = GameStates.CHECK_GUESS_HIGH
                    }
                    debug.d = 8d17
                
                GameStates.STORE_LOW_GUESS:    // Set register as bit 0, indicating guess is lower than lower bound.
                    alufn = b000000    //ADD 
                    asel = b01        // Bit 1
                    bsel = b10        // Bit 0
                    regfile_we = b1
                    regfile_wa = d10    // Temp Reg 3
                    if (advance) {
                        regfile_we = b1
                        game_fsm.d = GameStates.IDLE
                    }
                    debug.d = 8d18
                
                GameStates.CHECK_GUESS_HIGH:
                    alufn = b110101    // CMPLT, Check is High Reg < Guess Reg
                    asel = b00
                    bsel = b00        
                    regfile_we = b1
                    regfile_ra1 = d5    // High Bound Reg
                    regfile_ra2 = d6    // Guess Reg
                    regfile_wa = d10    // Temp Reg 3
                    wdsel = b00
                    rngsel = 0
                    if (advance) {
                        regfile_we = b1
                        game_fsm.d = GameStates.BRANCH_GUESS_HIGH
                    }
                    debug.d = 8d19
                
                GameStates.BRANCH_GUESS_HIGH: 
                    regfile_ra2 = d10    // Temp Reg 3, 1 means guess number is smaller than low bound
                    if (regfile_rd2[0]) {
                        game_fsm.d = GameStates.STORE_HIGH_GUESS
                    } else {
                        game_fsm.d = GameStates.STORE_IN_BETWEEN
                    }
                    debug.d = 8d20
                
                GameStates.STORE_HIGH_GUESS:    // Set register as bit 15, indicating guess is higher than high bound.
                    alufn = b000000    // ADD 
                    asel = b10        // Bit 0
                    bsel = b11        // Bit 15
                    regfile_we = b1
                    regfile_wa = d10    // Temp Reg 3
                    if (advance) {
                        regfile_we = b1
                        game_fsm.d = GameStates.IDLE
                    }
                    debug.d = 8d21
                
                GameStates.STORE_IN_BETWEEN:    // Set register as bit 2, indicating guess is in between bounds.
                    alufn = b000000    // ADD 
                    asel = b01        // Bit 1
                    bsel = b01        // Bit 1
                    regfile_we = b1
                    regfile_wa = d10    // Temp Reg 3
                    if (advance) {
                        regfile_we = b1
                        game_fsm.d = GameStates.IDLE
                    }
                    debug.d = 8d22
                
                GameStates.IDLE:
                    if (advance) {
                        game_fsm.d = GameStates.DECREASE_GAME_TIMER
                    } else if (p1_button_low) {
                        game_fsm.d = GameStates.SET_P1_BUTTON_LOW
                    } else if (p1_button_high) {
                        game_fsm.d = GameStates.SET_P1_BUTTON_HIGH
                    } else if (p1_button_between) {
                        game_fsm.d = GameStates.SET_P1_BUTTON_IN_BETWEEN
                    } else if (p2_button_high) {
                        game_fsm.d = GameStates.SET_P2_BUTTON_HIGH
                    } else if (p2_button_low) {
                        game_fsm.d = GameStates.SET_P2_BUTTON_LOW
                    } else if (p2_button_between) {
                        game_fsm.d = GameStates.SET_P2_BUTTON_IN_BETWEEN
                    }
                    debug.d = 8d23
                
                GameStates.SET_P1_BUTTON_LOW:
                    alufn = b000000    // ADD 
                    asel = b10        // Bit 0
                    bsel = b01        // Bit 1
                    regfile_we = b1
                    regfile_wa = d2    // P1 Button Reg
                    wdsel = b00
                    if (advance) {
                        regfile_we = b1
                        game_fsm.d = GameStates.IDLE
                    }
                    debug.d = 8d24
                
                GameStates.SET_P1_BUTTON_HIGH:
                    alufn = b000000    // ADD 
                    asel = b10        // Bit 0
                    bsel = b11        // Bit 15
                    regfile_we = b1
                    regfile_wa = d2    // P1 Button Reg
                    wdsel = b00
                    if (advance) {
                        regfile_we = b1
                        game_fsm.d = GameStates.IDLE
                    }
                    debug.d = 8d25
                
                GameStates.SET_P1_BUTTON_IN_BETWEEN:
                    alufn = b000000    // ADD 
                    asel = b01        // Bit 1
                    bsel = b01        // Bit 1
                    regfile_we = b1
                    regfile_wa = d2    // P1 Button Reg
                    wdsel = b00
                    if (advance) {
                        regfile_we = b1
                        game_fsm.d = GameStates.IDLE
                    }
                    debug.d = 8d26
                
                GameStates.SET_P2_BUTTON_LOW:
                    alufn = b000000    // ADD 
                    asel = b10        // Bit 0
                    bsel = b01        // Bit 1
                    regfile_we = b1
                    regfile_wa = d3    // P2 Button Reg
                    wdsel = b00
                    if (advance) {
                        regfile_we = b1
                        game_fsm.d = GameStates.IDLE
                    }
                    debug.d = 8d27
                
                GameStates.SET_P2_BUTTON_HIGH:
                    alufn = b000000    // ADD 
                    asel = b10        // Bit 0
                    bsel = b11        // Bit 15
                    regfile_we = b1
                    regfile_wa = d3    // P2 Button Reg
                    wdsel = b00
                    if (advance) {
                        regfile_we = b1
                        game_fsm.d = GameStates.IDLE
                    }
                    debug.d = 8d28
                
                GameStates.SET_P2_BUTTON_IN_BETWEEN:
                    alufn = b000000    // ADD 
                    asel = b01        // Bit 1
                    bsel = b01        // Bit 1
                    regfile_we = b1
                    regfile_wa = d3   // P2 Button Reg
                    wdsel = b00
                    if (advance) {
                        regfile_we = b1
                        game_fsm.d = GameStates.IDLE
                    }
                    debug.d = 8d29
                
                GameStates.DECREASE_GAME_TIMER:
                    alufn = b000001    // SUBTRACT
                    asel = b00
                    bsel = b01        // Bit 1
                    regfile_we = b1
                    regfile_wa = d7   // Timer Reg
                    regfile_ra1 = d7    // Timer Reg
                    wdsel = b00
                    game_fsm.d = GameStates.CHECK_TIMER
                    debug.d = 8d30
                
                GameStates.CHECK_TIMER:
                    alufn = b110011    // CMPEQ
                    asel = b00
                    bsel = b10        // Bit 0
                    regfile_we = b1
                    regfile_wa = d8   // Temp Reg to store Result of Comparison
                    regfile_ra1 = d7    // Timer Reg
                    wdsel = b00
                    if (advance) {
                        regfile_we = b1
                        game_fsm.d = GameStates.BRANCH_CHECK_TIMER
                    }
                    debug.d = 8d31
                
                GameStates.BRANCH_CHECK_TIMER:
                    regfile_ra2 = d8   // Temp Reg 1, storing value of comparison
                    if (regfile_rd2[0]) {    // If timer == 0, check inputs
                        game_fsm.d = GameStates.CHECK_P1_BUTTON_PRESS
                    } else {
                        game_fsm.d = GameStates.IDLE
                    }
                    debug.d = 8d32
                
                GameStates.CHECK_P1_BUTTON_PRESS:
                    alufn = b110011    // CMPEQ
                    asel = b00
                    bsel = b00        
                    regfile_we = b1
                    regfile_wa = d9   // Temp Reg 2 to store Result of Comparison
                    regfile_ra1 = d2    // P1 Button Count Reg
                    regfile_ra2 = d10    // Temp Reg 3, storing result of guess
                    wdsel = b00
                    if (advance) {
                        regfile_we = b1
                        game_fsm.d = GameStates.BRANCH_P1_BUTTON_PRESS
                    }
                    debug.d = 8d33
                
                GameStates.BRANCH_P1_BUTTON_PRESS:
                    regfile_ra2 = d9   // Temp Reg 2, storing value of comparison
                    if (regfile_rd2[0]) {    // If P1 input is same as result, increase P1 score
                        game_fsm.d = GameStates.INCREASE_P1_SCORE
                    } else {
                        game_fsm.d = GameStates.CHECK_P2_BUTTON_PRESS
                    }
                    debug.d = 8d34
                
                GameStates.INCREASE_P1_SCORE:
                    alufn = b000000    // ADD
                    asel = b00
                    bsel = b01    // Bit 1       
                    regfile_we = b1
                    regfile_wa = d0   // P1 Score Reg
                    regfile_ra1 = d0    // P1 Score Reg
                    wdsel = b00
                    game_fsm.d = GameStates.CHECK_P2_BUTTON_PRESS
                    debug.d = 8d35
                
                GameStates.CHECK_P2_BUTTON_PRESS:
                    alufn = b110011    // CMPEQ
                    asel = b00
                    bsel = b00        
                    regfile_we = b1
                    regfile_wa = d8   // Temp Reg 1 to store Result of Comparison
                    regfile_ra1 = d3    // P2 Button Count Reg
                    regfile_ra2 = d10    // Temp Reg 3, storing result of guess
                    wdsel = b00
                    if (advance) {
                        regfile_we = b1
                        game_fsm.d = GameStates.BRANCH_P2_BUTTON_PRESS
                    }
                    debug.d = 8d36
                
                GameStates.BRANCH_P2_BUTTON_PRESS:
                    regfile_ra2 = d8   // Temp Reg 1, storing value of comparison
                    if (regfile_rd2[0]) {    // If P2 input is same as result, increase P1 score
                        game_fsm.d = GameStates.INCREASE_P2_SCORE
                    } else {
                        game_fsm.d = GameStates.COMPARE_SCORES
                    }
                    debug.d = 8d37
                
                GameStates.INCREASE_P2_SCORE:
                    alufn = b000000    // ADD
                    asel = b00
                    bsel = b01    // Bit 1       
                    regfile_we = b1
                    regfile_wa = d1   // P2 Score Reg
                    regfile_ra1 = d1    // P2 Score Reg
                    wdsel = b00
                    game_fsm.d = GameStates.COMPARE_SCORES
                    debug.d = 8d38
                
                GameStates.COMPARE_SCORES:
                    alufn = b110101    // CMPLT, Check is P1 Score < P2 Score
                    asel = b00
                    bsel = b00
                    regfile_we = b1
                    regfile_wa = d8   // Temp Reg 1
                    regfile_ra1 = d0    // P1 Score Reg
                    regfile_ra2 = d1    // P2 Score Reg
                    wdsel = b00
                    if (advance) {
                        regfile_we = b1
                        game_fsm.d = GameStates.BRANCH_COMPARE_SCORES
                    }
                    debug.d = 8d39
                
                GameStates.BRANCH_COMPARE_SCORES:
                    regfile_ra2 = d8   // Temp Reg 1, storing value of comparison
                    if (regfile_rd2[0]) {    // If P1 Score < P2 Score, look at value of P2 Score
                        game_fsm.d = GameStates.CHECK_P2_SCORE
                    } else {
                        game_fsm.d = GameStates.CHECK_P1_SCORE
                    }
                    debug.d = 8d40
                
                GameStates.CHECK_P1_SCORE:
                    alufn = b110011    // CMPEQ
                    asel = b11        // Bit 5
                    bsel = b00        
                    regfile_we = b1
                    regfile_wa = d8   // Temp Reg 1 to store Result of Comparison
                    regfile_ra2 = d0    // P1 Score Reg
                    wdsel = b00
                    if (advance) {
                        regfile_we = b1
                        game_fsm.d = GameStates.BRANCH_CHECK_P1_SCORE
                    }
                    debug.d = 8d41
                
                GameStates.BRANCH_CHECK_P1_SCORE:
                    regfile_ra2 = d8   // Temp Reg 1, storing value of comparison
                    if (regfile_rd2[0]) {    // If P1 Score == 5, game over, go to check results
                        game_fsm.d = GameStates.CHECK_DRAW
                    } else {
                        game_fsm.d = GameStates.RESET_TIMER
                    }
                    debug.d = 8d42
                
                GameStates.CHECK_P2_SCORE:
                    alufn = b110011    // CMPEQ
                    asel = b11        // Bit 5
                    bsel = b00        
                    regfile_we = b1
                    regfile_wa = d8   // Temp Reg 1 to store Result of Comparison
                    regfile_ra2 = d1    // P2 Score Reg
                    wdsel = b00
                    if (advance) {
                        regfile_we = b1
                        game_fsm.d = GameStates.BRANCH_CHECK_P2_SCORE
                    }
                    debug.d = 8d43
                
                GameStates.BRANCH_CHECK_P2_SCORE:
                    regfile_ra2 = d8   // Temp Reg 1, storing value of comparison
                    if (regfile_rd2[0]) {    // If P2 Score == 5, game over, go to check results
                        game_fsm.d = GameStates.CHECK_DRAW
                    } else {
                        game_fsm.d = GameStates.RESET_TIMER
                    }
                    debug.d = 8d44
                
                GameStates.CHECK_DRAW:
                    alufn = b110011    // CMPEQ
                    asel = b00
                    bsel = b00        
                    regfile_we = b1
                    regfile_wa = d8   // Temp Reg 1 to store Result of Comparison
                    regfile_ra1 = d0    // P1 Score Reg
                    regfile_ra2 = d1    // P2 Score Reg
                    wdsel = b00
                    if (advance) {
                        regfile_we = b1
                        game_fsm.d = GameStates.BRANCH_DRAW
                    }
                    debug.d = 8d45
                
                GameStates.BRANCH_DRAW:
                    regfile_ra2 = d8   // Temp Reg 1, storing value of comparison
                    if (regfile_rd2[0]) {    // If P1 Score == P2 Score, draw
                        wdsel = b10
                        regfile_we = b1
                        regfile_wa = b0        // P1 Score Register
                        game_fsm.d = GameStates.DRAW
                    } else {
                        game_fsm.d = GameStates.CHECK_WINNER
                    }
                    debug.d = 8d46
                
                GameStates.DRAW:
                    wdsel = b10                // Draw Signal
                    regfile_we = 1 
                    regfile_wa = d1          // P2 score reg
                    if (advance) {
                        regfile_we = b1
                        game_fsm.d = GameStates.GAMEOVER
                    }
                    debug.d = 8d47 
                
                GameStates.CHECK_WINNER:
                    alufn = b110101    // CMPLT, Check if P1 Score < P2 Score
                    asel = b00
                    bsel = b00        
                    regfile_we = b1
                    regfile_wa = d8   // Temp Reg 1 to store Result of Comparison
                    regfile_ra1 = d0    // P1 Score Reg
                    regfile_ra2 = d1    // P2 Score Reg
                    wdsel = b00
                    if (advance) {
                        regfile_we = b1
                        game_fsm.d = GameStates.BRANCH_WINNER
                    }
                    debug.d = 8d48
                
                GameStates.BRANCH_WINNER:
                    regfile_ra2 = d8   // Temp Reg 1, storing value of comparison
                    if (regfile_rd2[0]) {    // If P2 Wins
                        game_fsm.d = GameStates.P2_WINS
                    } else {
                        game_fsm.d = GameStates.P1_WINS
                    }
                    debug.d = 8d49
                
                GameStates.P2_WINS:
                    regfile_we = 1
                    regfile_wa = d1     // P2 score reg
                    wdsel = b01           // winning signal
                    if (advance) {
                        regfile_we = b1
                        game_fsm.d = GameStates.P1_LOSE
                    }
                    debug.d = 8d50
                
                GameStates.P1_LOSE:
                    regfile_we = 1
                    regfile_wa = d0     // P1 score reg
                    wdsel = b11           // losing signal
                    if (advance) {
                        regfile_we = b1
                        game_fsm.d = GameStates.GAMEOVER
                    }
                    debug.d = 8d51
                
                GameStates.P1_WINS:
                    regfile_we = 1
                    regfile_wa = d0     // P1 score reg
                    wdsel = b01           // winning signal
                    if (advance) {
                        regfile_we = b1
                        game_fsm.d = GameStates.P2_LOSE
                    }
                    debug.d = 8d52
                
                GameStates.P2_LOSE:
                    regfile_we = 1
                    regfile_wa = d1     // P2 score reg
                    wdsel = b11           // losing signal
                    if (advance) {
                        regfile_we = b1
                        game_fsm.d = GameStates.GAMEOVER
                    }
                    debug.d = 8d53
                
                GameStates.GAMEOVER:
                    game_fsm.d = GameStates.GAMEOVER
                    debug.d = 8d54
            }
        }
    }
